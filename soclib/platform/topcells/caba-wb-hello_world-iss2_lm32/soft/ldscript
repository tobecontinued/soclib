/****************************************
 * linkscript for lm32_hello_world
 *
 * Maintainers: tarik.graba@telecom-paristech.fr
 * 
 ****************************************/


OUTPUT_FORMAT("elf32-lm32")
OUTPUT_ARCH(lm32)
ENTRY(_start)
GROUP(-lgcc)

MEMORY {
    rom (rx) : ORIGIN = 0, LENGTH = 1M
    ram (!rx): ORIGIN = 0x10000000 , LENGTH = 16M
    }

__stack = ORIGIN(ram) +  LENGTH(ram)  - 8;


SECTIONS
{ 
/* The program will be loaded in rom
 * data and bss should be moved to ram */
    
    . = ORIGIN(rom) ;

    .boot   :
    {
        boot.o (.boot)
    } > rom
        . = ALIGN(4);
    .text	:
    {
        crt0.o	(.text)
               *(.text)
    } > rom

    . = ALIGN(4);
    .rodata : 
    { 
        *(.rodata*)
    } > rom

    __end_of_text__ = . ;

    . = ALIGN(4);
    .data : { 
            __data_start__ = . ;
            *(.data*) 
            __data_end__ = . ;
            } > ram AT> rom

    . = ALIGN(4);

    .bss : { 
        __bss_start__ = .;
        *(.bss) 
        *(COMMON)
        __bss_end__ = .;
    } > ram AT> rom

    . = ALIGN(4);

    _heap = .;
}

/*
 * vim: filetype=ld:
 */
