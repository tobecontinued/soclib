/*
 * SOCLIB_GPL_HEADER_BEGIN
 * 
 * This file is part of SoCLib, GNU GPLv2.
 * 
 * SoCLib is free software; you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation; version 2 of the License.
 * 
 * SoCLib is distributed in the hope that it will be useful, but
 * WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
 * General Public License for more details.
 * 
 * You should have received a copy of the GNU General Public License
 * along with SoCLib; if not, write to the Free Software
 * Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA
 * 02110-1301, USA.
 * 
 * SOCLIB_GPL_HEADER_END
 */

PHDRS
{
        reset PT_LOAD ;
        excep PT_LOAD ;
        text PT_LOAD ;
        data PT_LOAD ;
        heap PT_NULL ;
        stack PT_NULL ;
        bss PT_LOAD ;
}

SECTIONS
{
   . = 0x80000000;
   .excep : {
		*(.excep)
		*(.excep.*)
   } :excep

   . = 0x00000000;
   .reset : {
		*(.reset)
		*(.reset.*)
   } :reset

   . = 0x00400000;
   .text : {
      *(.text)
   } :text

   . = 0x10000000;
   .rodata : {
      *(.rodata)
      *(.rodata.*)
      . = ALIGN(4);
   } :data
   .data . : {
      *(.data)
   } :data
   .sdata : {
      *(.lit8)
      *(.lit4)
      *(.sdata)
   } :data
   . = ALIGN(4);
   _edata = .;

   .sbss  : {
      *(.sbss)
      *(.scommon)
   } :bss
   .bss   : {
      *(.bss)
      *(COMMON)
   } :bss
   . = ALIGN(4);
   _end = .;
   _heap = .;
   _stack = 0x10020000-16;
}
